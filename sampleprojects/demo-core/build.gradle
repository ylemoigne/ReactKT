/*
 * Copyright 2015 Yann Le Moigne
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin2js"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-js-library:$kotlin_version"
    compile project(":subprojects:w3c-dom-html")
    compile project(":subprojects:core")
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.outputFile = "${buildDir}/classes/main/${project.name}.js"
}

task webdist(type: Copy, dependsOn: tasks.build) {
    configurations.compile.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        from(zipTree(artifact.file)) {
            exclude 'META-INF/'
            exclude '*.meta.js'
            into "lib/${artifact.name}"
        }
    }
    from("$buildDir/classes/main") {
        exclude '*.meta.js'
    }
    from "$buildDir/resources/main"
    into "$buildDir/webdist"
}

tasks["classes"].dependsOn(tasks["compileKotlin2Js"])

